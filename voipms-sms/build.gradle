apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId 'net.kourlas.voipms_sms'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 124
        versionName '0.6.6'
    }
    flavorDimensions 'version', 'demo'
    productFlavors {
        primary {
            dimension 'version'
        }
        fdroid {
            dimension 'version'
            versionNameSuffix '-fdroid'
        }
        full {
            dimension 'demo'
            buildConfigField 'boolean', 'IS_DEMO', 'false'
            buildConfigField 'boolean', 'IS_DEMO_SENDING', 'false'
        }
        demoNotSending {
            dimension 'demo'
            applicationId 'net.kourlas.voipms_sms.demo'
            buildConfigField 'boolean', 'IS_DEMO', 'true'
            buildConfigField 'boolean', 'IS_DEMO_SENDING', 'false'
        }
        demoSending {
            dimension 'demo'
            applicationId 'net.kourlas.voipms_sms.demo'
            buildConfigField 'boolean', 'IS_DEMO', 'true'
            buildConfigField 'boolean', 'IS_DEMO_SENDING', 'true'
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        def isDemo = (names.contains('demoSending')
                      || names.contains('demoNotSending'))
        def isRelease = variant.buildType.name == 'release'
        def isPrimary = names.contains('primary')
        if (isDemo && (isPrimary || isRelease)) {
            variant.setIgnore(true)
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        primary.java.srcDirs += 'src/primary/kotlin'
        fdroid.java.srcDirs += 'src/fdroid/kotlin'
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Support libraries
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.core:core:1.3.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'

    // Firebase libraries
    primaryImplementation 'com.google.firebase:firebase-analytics:17.4.3'
    primaryImplementation 'com.google.firebase:firebase-appindexing:19.1.0'
    primaryImplementation 'com.google.firebase:firebase-core:17.4.3'
    primaryImplementation 'com.google.firebase:firebase-crashlytics:17.0.1'
    primaryImplementation 'com.google.firebase:firebase-messaging:20.2.0'

    // Additional libraries and fixes
    implementation 'com.takisoft.preferencex:preferencex-datetimepicker:1.1.0'
    implementation 'com.takisoft.preferencex:preferencex-ringtone:1.1.0'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'de.adorsys.android:securestoragelibrary:1.2.2'
    implementation 'com.google.code.gson:gson:2.8.6'
}

apply plugin: 'kotlin-android-extensions'

// Remove from F-Droid builds
tasks.whenTaskAdded {task ->
    if (task.name.contains('Fdroid') && task.name.contains('GoogleServices')) {
        task.enabled = false
    }
}
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
