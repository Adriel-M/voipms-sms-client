apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// Should be removed in F-Droid builds
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId 'net.kourlas.voipms_sms'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 123
        versionName '0.6.5'
    }
    flavorDimensions 'version'
    productFlavors {
        primary {
            dimension 'version'
        }
        fdroid {
            dimension 'version'
            versionNameSuffix '-fdroid'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        primary.java.srcDirs += 'src/primary/kotlin'
        fdroid.java.srcDirs += 'src/fdroid/kotlin'
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Support libraries
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'

    // Firebase libraries
    primaryImplementation 'com.google.firebase:firebase-appindexing:19.0.0'
    primaryImplementation 'com.google.firebase:firebase-core:17.2.1'
    primaryImplementation 'com.google.firebase:firebase-messaging:20.1.0'
    primaryImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // Additional libraries and fixes
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'com.takisoft.preferencex:preferencex-ringtone:1.1.0'
    implementation 'com.takisoft.preferencex:preferencex-datetimepicker:1.1.0'
}

apply plugin: 'kotlin-android-extensions'

// Should be removed in F-Droid builds
tasks.whenTaskAdded {task ->
    if (task.name.contains('Fdroid')
        && (task.name.contains('GoogleServices')
            || task.name.contains('fabric'))) {
        task.enabled = false
    }
}
apply plugin: 'com.google.gms.google-services'
